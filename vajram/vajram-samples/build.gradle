plugins {
    id 'java-library'

    id 'com.flipkart.krystal' version "${krystal_version}"
    id 'com.flipkart.java.code.standard' version '4.1'

    //Needed only if you want your project to be modularized using JPMS
    id 'org.gradlex.extra-java-module-info' version '1.11'
}

dependencies {
    /** START: Krystal dependencies **/
    implementation platform("com.flipkart.krystal:krystal-bom:${krystal_version}")
    annotationProcessor platform("com.flipkart.krystal:krystal-bom:${krystal_version}")
    testAnnotationProcessor platform("com.flipkart.krystal:krystal-bom:${krystal_version}")

    implementation 'com.flipkart.krystal:vajram-java-sdk'
    implementation 'com.flipkart.krystal:krystal-visualization'

    annotationProcessor 'com.flipkart.krystal:vajram-codegen'
    /** END: Krystal dependencies **/

    implementation 'com.google.guava:guava:33.4.8-jre'
    implementation 'jakarta.inject:jakarta.inject-api'

    compileOnly "org.projectlombok:lombok:$lombok_version"
    compileOnly 'com.google.auto.value:auto-value-annotations:1.11.0'
    annotationProcessor 'com.google.auto.value:auto-value:1.11.0'

    /** START: Test dependencies **/
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'

    testImplementation platform('org.assertj:assertj-bom:3.25.2')
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'com.flipkart.krystal:vajram-krystex'
    testImplementation 'com.flipkart.krystal:vajram-guice'

    testImplementation platform('org.mockito:mockito-bom:5.17.0')
    testImplementation 'org.mockito:mockito-core'

    testImplementation 'com.fasterxml.jackson.core:jackson-annotations'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    /** END: Test dependencies **/
}

test {
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "4g" // maximum heap size
}

checkerFramework {
    excludeTests = true
}

//Needed only if you want your project to be modularized using JPMS
/** START: JPMS compatibility configurations **/
configurations.configureEach {
    exclude group: 'com.google.guava', module: 'listenablefuture'
}
extraJavaModuleInfo {
    module('com.google.auto.value:auto-value-annotations', 'com.google.auto.value.annotations')
    module('com.flipkart.krystal:vajram-codegen', 'com.flipkart.krystal.vajram.codegen')
    module('com.google.code.findbugs:jsr305', 'com.google.code.findbugs.jsr305')
    module('com.github.kevinstern:software-and-algorithms', 'com.github.kevinstern.software.and.algorithms')
    module('javax.inject:javax.inject', 'javax.inject')
    module('com.google.auto:auto-common', 'com.google.auto.common')
    module('com.google.auto.value:auto-value', 'com.google.auto.value')
    module('aopalliance:aopalliance', 'aopalliance')
    module('javax.cache:cache-api', 'javax.cache.api')
    module('org.hdrhistogram:HdrHistogram', 'org.hdrhistogram')
    module('org.latencyutils:LatencyUtils', 'org.latencyutils')

    knownModule('org.slf4j:slf4j-api', 'org.slf4j')
    knownModule('jakarta.inject:jakarta.inject-api', 'jakarta.inject')
    knownModule('com.google.auto.service:auto-service-annotations', 'com.google.auto.service')
    knownModule('org.checkerframework:checker-qual', 'org.checkerframework.checker.qual')
    knownModule('com.flipkart.krystal:krystal-codegen-common', 'flipkart.krystal.codegen.common')
    knownModule('com.flipkart.krystal:vajram-codegen-common', 'flipkart.krystal.vajram.codegen.common')
    knownModule('com.flipkart.krystal:vajram-java-sdk', 'flipkart.krystal.vajram')
    knownModule('com.google.guava:guava', 'com.google.common')
    knownModule('com.squareup:javapoet', 'com.squareup.javapoet')
    knownModule('org.projectlombok:lombok', 'lombok')
    knownModule('com.google.j2objc:j2objc-annotations', 'com.google.j2objc.annotations')
    knownModule('org.reflections:reflections', 'org.reflections')
    knownModule('org.javassist:javassist', 'org.javassist')

}
/** END: JPMS compatibility configurations **/